global u8 restyled.Palette
global u8 restyled.Anim
global u8 restyled.Mira

function u32 getCharacterColorsForControllerLEDs(u8 character)
{


	if (u8[0xffffe654] == 0 && character == CHARACTER_SONIC)
		return 0xB448FC
		
	return base.getCharacterColorsForControllerLEDs(character)
}


function bool s1Restyled.isMiracleSonic()
{
	return (restyled.Anim == 1 && restyled.Mira == 0 && restyled.Palette == 0x03)
}

function void Init()
{
	if (restyled.Anim == 0x01 && u8[0xffffe654] == 0)
	{
		System.loadExternalRawData("SCD_walk", 0x12aee)
		System.loadExternalRawData("SCD_walk_super", 0x12c7a)
		System.loadExternalRawData("SCD_roll", 0x12b03)
		System.loadExternalRawData("SCD_idle", 0x12b20)
		System.loadExternalRawData("SCD_fan", 0x12bd1)
	}
	base.Init()
}



function string getCharacterPaletteKey(u8 character)
{
	if (character == CHARACTER_SONIC && u8[0xffffe654] == 0)
	{
		if (restyled.Palette == 0x00)
			return "S1"
		if (restyled.Palette == 0x01)
			return "S1-DISK"
		if (restyled.Palette == 0x02)
			return "S1E"
		if (restyled.Palette == 0x03)
		{
			if (s1Restyled.isMiracleSonic())
				return "SCD-MIRACLE"
			else
				return "SCD"
		}
		if (restyled.Palette == 0x04)
			return "S2"
		if (restyled.Palette == 0x05)
			return "S3"
		if (restyled.Palette == 0x06)
			return "S3-MORCOL"
		if (restyled.Palette == 0x07)
			return "S3DB"
		if (restyled.Palette == 0x08)
			return "SPA"
		if (restyled.Palette == 0x09)
			return "MODGEN"
		if (restyled.Palette == 0x0a)
			return "1999"			
		if (restyled.Palette == 0x0b)
			return "ROSIE"
		if (restyled.Palette == 0x0c)
			return "JADE"
		if (restyled.Palette == 0x0d)
			return "OWEN"
		if (restyled.Palette == 0x0e)
			return "CHRIS"
		if (restyled.Palette == 0x0f)
			return "PIXY"
		if (restyled.Palette == 0x10)
			return "FREEPLAN"
		if (restyled.Palette == 0x11)
			return "VADA"
		if (restyled.Palette == 0x12)
			return "ANDREW"
		if (restyled.Palette == 0x13)
			return "TID"
		if (restyled.Palette == 0x14)
			return "SMOKE"		
		if (restyled.Palette == 0x15)
			return "RAINE"			
		if (restyled.Palette == 0x16)
			return "CA22"
		if (restyled.Palette == 0x17)
			return "SKETCH"	
		if (restyled.Palette == 0x18)
			return "PRTL"
		if (restyled.Palette == 0x19)
			return "SCARE"
		if (restyled.Palette == 0x1a)
			return "FSTVE"
		if (restyled.Palette == 0x1b)
			return "CUSTOM"			
		else //Failsafe
			return "00"
	}

	return base.getCharacterPaletteKey(character)
}

function string getRestyledCharacterAnimationKey()
{
	if (restyled.Anim == 0x00)
		return "s1"
	if (restyled.Anim == 0x01)
	{
		if (super.active)
		{
			if (s1Restyled.isMiracleSonic())
				return "cd"
			else
				return "miracle"
		}
		else
		{
			return "cd"
		}
	}		
	if (restyled.Anim == 0x02)
		return "23"
	if (restyled.Anim == 0x03)
		return "misc"
	else //Failsafe
		return "s1"
}

function string getCharacterSignpostSpriteKey(u8 character)
{
	if (character == CHARACTER_SONIC)
	{
		if (restyled.Anim == 0x00)
			return "signpost_sonic"
		if (restyled.Anim == 0x01)
			if (s1Restyled.isMiracleSonic())
				return "signpost_sonic_cd"
			else
				return "signpost_sonic_miracle"
		if (restyled.Anim == 0x02)
			return "signpost_sonic_23"
		if (restyled.Anim == 0x03)
			return "signpost_sonic_misc"
	}
	
	return base.getCharacterSignpostSpriteKey(character)
}

function bool Standalone.drawCharacterSprite(u8 character, u8 variant, s16 px, s16 py, bool asSmallGhost)
{
	if (character != CHARACTER_SONIC || variant == 2 || u8[0xffffe654] != 0)
		return base.Standalone.drawCharacterSprite(character, variant, px, py, asSmallGhost)

	bool isPlayer1 = (variant == 0)
	u32 characterAddress = isPlayer1 ? 0xffffb000 : 0xffffb04a

	if (level.vertical_wrap == 0xff00)
	{
		// For vertically wrapping levels, "normalize" py into interval [-move_area.bottom.target * 3/4, -move_area.bottom.target * 1/4]
		py &= level.height.bitmask
		if (py > move_area.bottom.target * 3/4 && move_area.bottom.target >= 0xe0)	// move_area.bottom.target is very low in DEZ boss act
			py -= move_area.bottom.target
	}

	u8 animationSprite = char.animation.sprite
	u16 animationSpriteEx = Standalone.getModdedAnimationSpriteEx(character, animationSprite)
	u8 flags = (char.render_flags & 0x03)
	u8 angle = 0
	u64 key = 0
	u8 rotationMode = 0

	if (animationSpriteEx == animationSprite)	// Last check is only false if modded scripts made their own changes
	{
		// Special handling for Drop Dash & Super Peel-Out
		if (char.state == char.state.SONIC_DROPDASH)
		{
			animationSpriteEx = CHAR_ANIMSPRITE_SONIC_DROPDASH + ((level.framecounter >> 1) & 0x01)
		}
		else if (!super.active && animationSprite >= 0x21 && animationSprite <= 0x30)
		{
			if (sonic.fastrunanim.timer > 0)
			{
				animationSpriteEx = CHAR_ANIMSPRITE_SONIC_PEELOUT + (level.framecounter & 0x03)

				if (!Game.getSetting(SETTING_SMOOTH_ROTATION))
				{
					angle = (animationSprite - 0x21) / 4 * 0xe0
					if (char.flags & char.flag.FACING_LEFT)
						angle = -angle
					animationSprite = 0x21 + (animationSprite - 0x21) % 4
				}
			}
		}
	}

	if (Game.getSetting(SETTING_SMOOTH_ROTATION))
	{
		// Character
		if (animationSprite >= 0x01 && animationSprite <= 0x20)
		{
			animationSprite = 0x01 + (animationSprite - 0x01) % 8
			rotationMode = 1
		}
		else if (animationSprite >= 0x21 && animationSprite <= 0x30)
		{
			animationSprite = 0x21 + (animationSprite - 0x21) % 4
			rotationMode = 1
		}
		else if (animationSprite >= 0x78 && animationSprite <= 0x7f)
		{
			animationSprite = 0x78
			rotationMode = 2
		}
		else if (character == CHARACTER_TAILS && animationSprite >= 0xc3 && animationSprite <= 0xca)
		{
			animationSprite = 0xc3 + (animationSprite - 0xc3) % 2
			rotationMode = 1
		}
		else if (character == CHARACTER_KNUCKLES && animationSprite == 0xc0)
		{
			// Only for DDZ
			rotationMode = 1
		}

		if (rotationMode != 0)
		{
			s8 oldRotation = isPlayer1 ? oldRotationPlayer1 : oldRotationPlayer2
			flags = char.flags & char.flag.FACING_LEFT		// This really has to be "char.flags", not "char.render_flags"

			angle = char.rotation
			if (rotationMode == 1)
			{
				if (abs(s8(char.rotation)) <= 0x10 && abs(s8(oldRotation)) <= 0x10)
				{
					angle = 0
				}

				if (angle != char.rotation)
				{
					s8 diff = angle - oldRotation
					angle = oldRotation + clamp(diff, -3, 3)
				}
			}
		}

		if (isPlayer1)
			oldRotationPlayer1 = angle
		else
			oldRotationPlayer2 = angle
	}
	else
	{
		// Classic rotation
		if ((char.state == char.state.RUNNING) && (animationSpriteEx < 0x100))
		{
			u8 startingFrame = 0
			u8 frameCount 	 = 0
			if (animationSprite >= 0x01 && animationSprite <= 0x20)
			{
				startingFrame = 0x01
				frameCount = 8
			}
			else if (animationSprite >= 0x21 && animationSprite <= 0x30)
			{
				startingFrame = 0x21
				frameCount = 4
			}

			if ((startingFrame != 0) && (frameCount != 0))
			{
				angle = (animationSprite - startingFrame) / frameCount * 0xe0 * ((char.flags & char.flag.FACING_LEFT) == (global.inv_gravity == 0) ? -1 : 1)
				animationSprite = startingFrame + (animationSprite - startingFrame) % frameCount
			}
		}

		// Nothing special for vine rotation sprites because the sprites for them already exist
	}
	
	key = Standalone.getModdedAnimationSpriteKey(character, animationSprite)
	if (animationSpriteEx >= 0x100)
	{
		// Special handling for Drop Dash & Super Peel-Out
		if (animationSpriteEx >= CHAR_ANIMSPRITE_SONIC_PEELOUT)
		{
			if (restyled.Anim == 0)
				key = stringformat("sonic_peelout_%d", animationSpriteEx - CHAR_ANIMSPRITE_SONIC_PEELOUT)
			else
				key = stringformat("sonic_peelout_%s_%d", getRestyledCharacterAnimationKey(), animationSpriteEx - CHAR_ANIMSPRITE_SONIC_PEELOUT)
		}
		else
			key = stringformat("sonic_dropdash_%d", animationSpriteEx - CHAR_ANIMSPRITE_SONIC_DROPDASH)
	}
	
	timeattack.animSpriteEx = animationSpriteEx

	if (key == 0)
	{
		if (character == CHARACTER_SONIC)
		{
			if (restyled.Anim == 0)
				key = stringformat(super.active ? "character_supersonic_0x%02x" : "character_sonic_0x%02x", animationSprite)
			else
			{
				u64 regularkey = stringformat("character_sonic_%s_0x%02x", getRestyledCharacterAnimationKey(), animationSprite)
				u64 superkey = stringformat("character_supersonic_%s_0x%02x", getRestyledCharacterAnimationKey(), animationSprite)
				key = (super.active ? superkey : regularkey)
			}
		}

		if (!Renderer.hasCustomSprite(key))
		{
			u32 sourceBase    = (character == CHARACTER_SONIC) ? ((animationSprite >= 0xda) ? 0x140060 : 0x100000) : (character == CHARACTER_TAILS) ? ((animationSprite >= 0xd1) ? 0x143d00 : 0x3200e0) : 0x1200e0
			u32 tableAddress  = (character == CHARACTER_SONIC) ? (super.active ? 0x148378 : 0x148182) : (character == CHARACTER_TAILS) ? 0x14a08a : 0x14bd0a
			u32 mappingOffset = (character == CHARACTER_SONIC) ? (super.active ? 0x146816 : 0x146620) : (character == CHARACTER_TAILS) ? 0x148eb8 : 0x14a8d6		// Not really necessary here, we could also use "char.mapping_offset"

			key = Renderer.setupCustomCharacterSprite(sourceBase, tableAddress, mappingOffset, animationSprite, 0x00)
		}
	}

	u16 renderQueue = 0xa000 - char.sprite_priority
	u8 atex = (char.sprite_attributes >> 9) & 0x30
	if (EXTENDED_CHARACTER_PALETTES)
		atex = 0x40 + character * 0x20

	if (char.sprite_attributes & 0x8000)
		flags |= SPRITE_FLAG_PRIO

	if (rotationMode != 0 && global.inv_gravity)
	{
		// Correction for inverse gravity
		angle = 128 - angle
		flags ^= SPRITE_FLAG_FLIP_X
	}

	// Render character
	if (asSmallGhost)
		Renderer.drawCustomSprite(key, px, py, atex, flags | SPRITE_FLAG_PRIO, 0xa800, angle, 0xc0ffffff, 0x8000)
	else
	{
		if (char.state == char.state.GOT_HURT_PANIC && super.active)
		{
			angle = char.rotation
			
			if (char.flags & char.flag.IN_AIR)
			{
				char.hitbox_extends.x = char.hitbox.x.ROLLING
				char.hitbox_extends.y = char.hitbox.y.ROLLING
				char.flags |= char.flag.ROLLING
				char.state = char.state.ROLLING
			}
			
			py -= 8
		}
		Renderer.drawCustomSprite(key, px, py, atex, flags, renderQueue, angle, 255)
	}

	bool useAfterImages
	if (isPlayer1)
	{
		if ((super.active == 0xff) || (super.active.tails == 0xff))
		{
			useAfterImages = true
		}
		else
		{
			if (Game.getSetting(SETTING_SPEEDUP_AFTERIMGS))
			{
				useAfterImages = ((u8[characterAddress + 0x2b] & char.bonus.SPEED_UP) != 0)
			}
		}
	}

	if (useAfterImages)
	{
		// Additional offset for Sonic charging a Super Peel-Out (either Hyper Sonic or with Speed Shoes)
		s16 afterImagesOffsetX = 0
		if (char.character == CHARACTER_SONIC && char.spindash == 0x80)
		{
			afterImagesOffsetX = (char.groundspeed >> 7) * ((char.flags & char.flag.FACING_LEFT) ? 1 : -1)
		}

		for (s8 i = 3; i > 0; --i)
		{
			A1 = 0xffffe500 + u8(posbackup.offset.player1 - (i * 8 + 4))
			s16 px0 = u16[A1] - camera.foreground.x.u16 + (i * afterImagesOffsetX / 4)
			s16 py0 = u16[A1+2] - camera.foreground.y.u16
			
			if (char.state == char.state.GOT_HURT_PANIC && super.active)
				py0 -= 8
			
			Renderer.drawCustomSprite(key, px0, py0, atex, flags, renderQueue - i, angle, 192 - i * 32)
		}
	}

	// Prevent emulator-like rendering
	return true
}

//# address-hook(0x012600) end(0x01286c)
//# translated(0x012a2a) end(0x012a6e)
function void UpdateSonicAnimation()
{
	if (char.state == char.state.COWERING && super.active && u8[0xffffe654] == 0)
		Restyled.UpdateSuperCrouchAnimation()
	else
		base.UpdateSonicAnimation()
}

function void Restyled.UpdateSuperCrouchAnimation()
{
	if (char.state != char.state.former)
	{
		char.state.former = char.state
		char.animation.frame = 0
		char.animation.timer = 0
		char.flags &= ~char.flag.PUSHING
	}
		
	if (char.animation.timer >= 6)
	{
		char.animation.sprite = 0x9c
	}
	else
	{
		char.animation.sprite = 0x9b
		++char.animation.timer
	}

}

function string getCharacterBluespherePaletteKey(u8 character)
{
	if (character == CHARACTER_SONIC)
	{
		return getCharacterPaletteKey(character)
	}
	
	return base.getCharacterBluespherePaletteKey(character)
}

// renderhooks
function bool Standalone.onWriteToSpriteTable(s16 px, s16 py, u16 renderQueue)
{
	// data select portrait
	if (objA0.update_address == 0x00d42c)
	{	
		//bool isValidPortrait = (objA0.animation.frame == 0x19 || objA0.animation.frame == 0x23)
		bool isKnuckles = (u16[A0 + 0x34] == 3 || u16[A0 + 0x34] == 4)
		if (objA0.compound.count == 2 && !isKnuckles)
		{
			D0.u8 = dataselect.slot_selected - 1
			if (D0.u8 == objA0.flags2e)
			{
				bool drawSprite = false
			
				string key = "dataSel_savePortrait_sonic"
				
				if (u16[A0 + 0x34] <= 1)
				{
					drawSprite = true
					
					if (objA0.value3b == 3)
					{
						key = "dataSel_savePortrait_end"
						
						super.palettefx.state = 0
						loadCharacterPalette(CHARACTER_SONIC, 0x802000, 0)
					}
					else if (objA0.value3b == 2)
					{
						key = (s1Restyled.isMiracleSonic() ? "dataSel_savePortrait_sonicSuper_cd" : "dataSel_savePortrait_sonicSuper")

						if (super.palettefx.state == 0)
							super.palettefx.state = 1
						
						UpdatePaletteEffects.SuperForm.moddingVersion(1)
					}
					else 
					{
						super.palettefx.state = 0
						loadCharacterPalette(CHARACTER_SONIC, 0x802000, 0)
					}
				}

				if (drawSprite)
				{
					// zero normal ending portrait vram
					VDP.setupVRAMWrite(0x7900)
					VDP.zeroVRAM(0x1180)
					
					// zero best ending portrait vram
					VDP.setupVRAMWrite(0x8a80)
					VDP.zeroVRAM(0x08c0)
					
					SpriteHandle portrait = Renderer.addSpriteHandle(key, px, py, renderQueue)
					portrait.setFlags(SPRITE_FLAG_PRIO)
					if (key == "dataSel_savePortrait_sonicSuper" || key == "dataSel_savePortrait_sonicSuper_cd")
						portrait.setPaletteOffset(0x40)
				}
			}
		}
	}
	
	// Sonic in Data Select screen character selection
	// The arrows and big portraits are compound sprites for this object, so set objA0.animation.sprite to an empty frame instead of "return true" to not skip rendering those.
	if (objA0.update_address == 0x00d30c || objA0.update_address == 0x00d42c)
	{
		u16 characters = (objA0.update_address == 0x00d30c) ? dataselect.nosave_characters : u16[A0 + 0x34]
		// le raine code :0 (handles the data select lives and continues
		A1 = u32[A0 + 0x30]
		bool saveGameSlotUsed = (u8[A1] & 0x80) == 0 && objA0.update_address == 0x00d42c
		if (characters <= CHARS_SONIC_ALONE && saveGameSlotUsed)
		{
			SpriteHandle sonic = Renderer.addSpriteHandle("dataselect_icons_sonic", px-0x10, py+0x08, renderQueue)
			sonic.setFlags(SPRITE_FLAG_PRIO)
			sonic.setPalette(getCharacterPaletteKey(CHARACTER_SONIC), 0)
		}
		if (characters == CHARS_SONIC_AND_TAILS)
		{
			// Sonic & Tails
			SpriteHandle sonic = Renderer.addSpriteHandle(getCharacterSelectSpriteKey(CHARACTER_SONIC), px-9, py-20, renderQueue + 1)
			sonic.setFlags(SPRITE_FLAG_PRIO | SPRITE_FLAG_FLIP_X)
			sonic.setPrimaryPalette(getCharacterPaletteKey(CHARACTER_SONIC), 0)
		}
		else if (characters == CHARS_SONIC_ALONE)
		{
			// Sonic alone
			SpriteHandle sonic = Renderer.addSpriteHandle(getCharacterSelectSpriteKey(CHARACTER_SONIC), px-3, py-20, renderQueue + 1)
			sonic.setFlags(SPRITE_FLAG_PRIO | SPRITE_FLAG_FLIP_X)
			sonic.setPalette(getCharacterPaletteKey(CHARACTER_SONIC), 0)
		}
	}
	
	return base.Standalone.onWriteToSpriteTable(px, py, renderQueue)
}

// this function eliminates the base game sonic lives and continues icons. (from ramona :O)
//# address-hook(0x001506) end(0x001524)
function void fn001506()
{
	if (A1 == A2 + CHARS_SONIC_ALONE * 32)
	{
		A1 = A2 + CHARS_SONIC_AND_TAILS * 32
		base.fn001506()
		return
	}
	base.fn001506()
}